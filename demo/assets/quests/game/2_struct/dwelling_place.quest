# This file is a part of the LibMozok DEMO project, intended to demonstrate 
# how the library can be integrated into a popular game engine. Also, it is a
# good playground to test various features.

# ======================= Dwelling Place Structure =========================== #

version 1 0
project dwelling_place

type DwellingPlace : Map
type DwellingPlace_HeartTrial : Map
type DwellingPlace_HeartIsland : Map

object dwelling_place_ : DwellingPlace
object dwelling_place_heart_island_ : DwellingPlace_HeartIsland
object dwelling_place_heart_trial_ : DwellingPlace_HeartTrial

type DP_HeartCaveBloked : Fact
object dp_heart_trial_cave_blocked_ : DP_HeartCaveBloked, MapMovement
object dp_portal_1_password_ : Fact
object dp_portal_2_password_ : Fact

# ----------------------------- Big Heart Trial ------------------------------ #

type DP_Trial
	type DP_Trial_Puzzle : DP_Trial
	type DP_Trial_Fighting : DP_Trial
	type DP_Trial_Done : DP_Trial
object dp_trial_puzzle_ : DP_Trial_Puzzle
object dp_trial_fighting_ : DP_Trial_Fighting
object dp_trials_done_ : DP_Trial_Done

rel DP_Cur_Trial(DP_Trial)


action N/A DP_FinishPuzzleTrial:
	player : Player
	trials : DwellingPlace_HeartTrial
	cur : DP_Trial_Puzzle
	next : DP_Trial_Fighting
	pre At(player, trials)
		DP_Cur_Trial(cur)
	rem DP_Cur_Trial(cur)
	add DP_Cur_Trial(next)


action DP_FinishFightingTrial:
	player : Player
	trials : DwellingPlace_HeartTrial
	cur : DP_Trial_Fighting
	next : DP_Trial_Done
	pre At(player, trials)
		DP_Cur_Trial(cur)
	rem DP_Cur_Trial(cur)
	add DP_Cur_Trial(next)


action DP_FinishTrial:
	player : Player
	trials : DwellingPlace_HeartTrial
	island : DwellingPlace_HeartIsland
	done : DP_Trial_Done
	pre DP_Cur_Trial(done)
		At(player, trials)
	rem DP_Cur_Trial(done)
	add Path2(trials, island)


action DwellingPlace_ReadHeartCaveSign {map_movement}:
	player : Player
	dwelling_place : DwellingPlace
	cave_blocked : DP_HeartCaveBloked
	pre At(player, dwelling_place)
		Unaware(player, cave_blocked)
	rem Unaware(player, cave_blocked)
	add Know(player, cave_blocked)


action N/A DwellingPlace_OpenHeartCave {map_movement}:
	player : Player
	dwelling_place : DwellingPlace
	heart_trial : DwellingPlace_HeartTrial
	cave_blocked : DP_HeartCaveBloked
	pre At(player, dwelling_place)
		Know(player, cave_blocked)
		NotBeenUsed(cave_blocked)
	rem NotBeenUsed(cave_blocked)
	add Path2(dwelling_place, heart_trial)


action N/A DwellingPlace_BigHeartTrial {map_movement}:
	player : Player
	trial : DwellingPlace_HeartTrial
	island : DwellingPlace_HeartIsland
	pre At(player, trial)
	rem # none
	add Path2(trial, island)


# ----------------------------------- Init ----------------------------------- #

rlist InitDwellingPlace:
	Unaware(player_, dp_heart_trial_cave_blocked_)
	Unaware(player_, dp_portal_1_password_)
	Unaware(player_, dp_portal_2_password_)
	NotBeenUsed(dp_heart_trial_cave_blocked_)
	DP_Cur_Trial(dp_trial_puzzle_)

action InitDwellingPlace:
	pre # none
	rem # none
	add InitDwellingPlace()
